plugins {
    id 'net.minecrell.licenser' version '0.4.1'
    id 'org.ajoberstar.grgit' version '3.0.0'
    id 'com.github.ben-manes.versions' version '0.20.0'
    id 'java-gradle-plugin'
    id 'maven-publish'
}

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

group = 'net.minecraftforge.gradle'

gradlePlugin {
    plugins {
        userdev {
            id = group + '.userdev'
            implementationClass = 'net.minecraftforge.gradle.userdev.UserDevPlugin'
        }
        patcher {
            id = group + '.patcher'
            implementationClass = 'net.minecraftforge.gradle.patcher.PatcherPlugin'
        }
        mcp {
            id = group + '.mcp'
            implementationClass = 'net.minecraftforge.gradle.mcp.MCPPlugin'
        }
    }
}

sourceSets {
    common
    mcp
    patcher
    userdev
}

configurations {
    all {
        exclude group: 'org.ow2.asm'
    }
    mcpImplementation.extendsFrom commonImplementation
    patcherImplementation.extendsFrom commonImplementation
    userdevImplementation.extendsFrom mcpImplementation
    implementation.extendsFrom mcpImplementation, patcherImplementation, userdevImplementation
}

jar {
    from sourceSets.common.output
    from sourceSets.mcp.output
    from sourceSets.patcher.output
    from sourceSets.userdev.output
}

repositories {
    jcenter()
    maven { url = 'https://files.minecraftforge.net/maven' }
}

license {
    header = file('HEADER')
    ext {
        name = 'ForgeGradle'
        year = 2018
        fullname = 'Forge Development LLC'
    }
    exclude '**/*.properties'
    exclude 'net/minecraftforge/gradle/common/diff/'
    exclude 'net/minecraftforge/gradle/common/util/JavaVersionParser.java'
}

wrapper {
    gradleVersion = '4.9'
    distributionType = Wrapper.DistributionType.ALL
}

dependencies {
    commonImplementation gradleApi()
    commonImplementation 'com.cloudbees:diff4j:1.2'
    commonImplementation 'commons-io:commons-io:2.4'
    commonImplementation 'com.github.jponge:lzma-java:1.3' // replaces the LZMA binary
    commonImplementation 'com.nothome:javaxdelta:2.0.1' // GDIFF implementation for BinPatches
    commonImplementation 'com.google.code.gson:gson:2.8.5'
    commonImplementation 'com.google.guava:guava:26.0-jre'
    commonImplementation 'de.siegmar:fastcsv:1.0.2'
    commonImplementation 'net.minecraftforge:artifactural:1.0.+'
    commonImplementation 'org.apache.maven:maven-artifact:3.6.0'
    commonImplementation 'org.apache.httpcomponents:httpclient:4.3.3'
    commonImplementation 'net.minecraftforge:srgutils:0.2.12'

    mcpImplementation sourceSets.common.output
    patcherImplementation sourceSets.mcp.output
    patcherImplementation sourceSets.common.output
    userdevImplementation sourceSets.mcp.output
    userdevImplementation sourceSets.common.output
}

/* --- NOT NEEDED ---
//Gradle doesn't add it's own source when doing the API. So lets hack it in!
import org.gradle.plugins.ide.eclipse.model.*
import org.gradle.plugins.ide.eclipse.model.internal.*
project.extensions.eclipse.classpath.file.whenMerged { Classpath cp ->
    def gradleSrc = gradle.gradleHomeDir.absolutePath.replace(File.separator, '/') + '/src/'
    cp.entries.each { entry ->
        if ((entry in AbstractLibrary) && entry.library.file.name.startsWith('gradle-')) {
            def type = (entry.library.file.name =~ "^gradle(-(.*))?-(${gradle.gradleVersion})")[0][2]
            if (type == 'api') type = 'core-api' //Gradle name is different for cores
            if (type == '') type = 'core'
            entry.sourcePath = new FileReferenceFactory().fromPath(gradleSrc + type)
        }
    }
}
*/

publishing {
    publications {
        java(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url = 'https://files.axelandre42.ovh/maven/'
            authentication {
                basic(BasicAuthentication)
            }

            if (System.env.MAVEN_USERNAME && System.env.MAVEN_PASSWORD) {
                credentials {
                    username = System.env.USERNAME
                    password = System.env.PASSWORD
                }
            }
        }
    }
}